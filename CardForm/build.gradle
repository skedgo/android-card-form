apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

version '3.3.1'
group = 'com.braintreepayments'

android {
  compileSdkVersion 28
  buildToolsVersion '28.0.3'

  defaultConfig {
    minSdkVersion 15
    targetSdkVersion 28
    versionCode 39
    versionName version

    consumerProguardFiles 'proguard.cfg'
  }

  testOptions {
    unitTests {
      all {
        jvmArgs '-noverify'
      }
    }
  }

  lintOptions {
    textReport true
    textOutput 'stdout'
    abortOnError false
  }

  resourcePrefix 'bt_'
}

dependencies {
  implementation 'com.google.android.material:material:1.0.0'
  testImplementation 'junit:junit:4.12'
  testImplementation 'org.robolectric:robolectric:3.3.2'
  testImplementation 'org.mockito:mockito-core:2.8.9'
}

/* maven release */
task javadocs(type: Javadoc) {
  source = android.sourceSets.main.java.srcDirs
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
  failOnError false
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
  classifier = 'javadoc'
  from javadocs.destinationDir
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from android.sourceSets.main.java.srcDirs
}

artifacts {
  archives javadocsJar
  archives sourcesJar
}

//nexusStaging {
//    packageGroup = "com.braintreepayments"
//}

uploadArchives {
  repositories {
    mavenDeployer {
      repository(url: "file://${localMaven}")
    }
  }
}